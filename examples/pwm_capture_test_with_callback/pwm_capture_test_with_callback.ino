/**
 ** tc_lib library
 ** Copyright (C) 2015,2021
 **
 **   Antonio C. Domínguez Brito <antonio.dominguez@ulpgc.es>
 **     División de Robótica y Oceanografía Computacional <www.roc.siani.es>
 **     and Departamento de Informática y Sistemas <www.dis.ulpgc.es>
 **     Universidad de Las Palmas de Gran  Canaria (ULPGC) <www.ulpgc.es>
 **  
 ** This file is part of the tc_lib library.
 ** The tc_lib library is free software: you can redistribute it and/or modify
 ** it under  the  terms of  the GNU  General  Public  License  as  published  by
 ** the  Free Software Foundation, either  version  3  of  the  License,  or  any
 ** later version.
 ** 
 ** The  tc_lib library is distributed in the hope that  it  will  be  useful,
 ** but   WITHOUT   ANY WARRANTY;   without   even   the  implied   warranty   of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR  PURPOSE.  See  the  GNU  General
 ** Public License for more details.
 ** 
 ** You should have received a copy  (COPYING file) of  the  GNU  General  Public
 ** License along with the tc_lib library.
 ** If not, see: <http://www.gnu.org/licenses/>.
 **/
/*
 * File: pwm_capture_test.ino 
 * Description: This is an example to test how fast are the digital 
 * signals tc_lib is able to measure. For doing that it uses library
 * pwm_lib at https://github.com/antodom/pwm_lib.
 * Date: February 15th, 2021
 * Author: Antonio C. Dominguez-Brito <antonio.dominguez@ulpgc.es>
 * ROC-SIANI - Universidad de Las Palmas de Gran Canaria - Spain
 */

#include "tc_lib.h"
#include "pwm_lib.h"

using namespace arduino_due::pwm_lib;
using namespace arduino_due::tc_lib;

#define PWM_PERIOD_PIN_35 10000000 // hundredths of usecs (1e-8 secs)
#define PWM_DUTY_PIN_35    5000000 // hundredths of usecs (1e-8 secs)

#define PSEUDO_PERIOD 1000 // msecs

// defining pwm object using pin 35, pin PC3 mapped to pin 35 on the DUE
// this object uses PWM channel 0
pwm<pwm_pin::PWMH0_PC3> pwm_pin35;

// To measure PWM signals generated by the previous pwm objects, we will use
// a capture object of tc_lib library as "oscilloscope" probe:  capture_tc0. 
// IMPORTANT: Take into account that for TC0 (TC0 and channel 0) the TIOA0 is
// PB25, which is pin 2 for Arduino DUE, so capture_tc0's capture pin is pin
// 2. For the correspondence between all TIOA inputs for the different
// TC module channels, you should consult uC Atmel ATSAM3X8E datasheet
// in section "36. Timer Counter (TC)"), and the Arduino pin mapping 
// for the DUE.
// All in all, to meausure the pwm output in this example you should connect 
// the PWM output of pin 35 to capture_tc0 object pin 2.
// We are using a capture object to which we can provide a callback which get
// called each time a period is detected
capture_tc0_declaration_with_callback(); // TC0 and channel 0
auto& capture_pin2=capture_tc0;

volatile uint32_t period_counter=0;
void period_callback() { period_counter++; }

void setup() {
  // put your setup code here, to run once:

  Serial.begin(9600);

  // initialization of capture objects
  capture_pin2.config(
    (PWM_PERIOD_PIN_35/100)<<1,
    capture_tc0_t::DEFAULT_MAX_OVERRUNS,
    period_callback // <--- provided callback
                    // the callback prototype is void(*)()
  );

  // starting PWM signals
  pwm_pin35.start(PWM_PERIOD_PIN_35,PWM_DUTY_PIN_35);
}

void loop() {
  // put your main code here,to run repeatedly:

  delay(PSEUDO_PERIOD);

  uint32_t status,duty,period,pulses;

  auto now=millis();
  Serial.println("=======================================================================");
  Serial.print(now);
  Serial.print(": [PIN 35 -> PIN 2] "); 
  status=capture_pin2.get_duty_period_and_pulses(duty,period,pulses);
  Serial.print("duty: "); 
  Serial.print(
    static_cast<double>(duty)/
    static_cast<double>(capture_pin2.ticks_per_usec()),
    3
  );
  Serial.print(" usecs. period: ");
  Serial.print(
    static_cast<double>(period)/
    static_cast<double>(capture_pin2.ticks_per_usec()),
    3
  );
  Serial.print(" usecs. ");
  if(capture_pin2.is_overrun(status)) Serial.print("[overrun]");
  if(capture_pin2.is_stopped(status)) Serial.print("[stopped]");  
  Serial.println();   
  Serial.print(now);
  Serial.print(": [PIN 35 -> PIN 2] pulses: "); Serial.print(pulses);
  Serial.print(" period counter: "); Serial.print(period_counter);
  Serial.println();   
  Serial.println("=======================================================================");
}

